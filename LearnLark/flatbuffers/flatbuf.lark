start: include_decl* ( namespace_decl | type_decl | enum_decl | root_decl | attribute_decl )*

include_decl: INCLUDE STRING_LITERAL SEMICOLON

namespace_decl: NAMESPACE IDENTIFIER ( DOT IDENTIFIER )* SEMICOLON

attribute_decl: ATTRIBUTE STRING_LITERAL SEMICOLON

type_decl: ( TABLE | STRUCT ) type_decl_name metadata type_field_open field_decl* type_field_close
type_field_open: OPEN_BLOCK
type_field_close: CLOSE_BLOCK

enum_decl: ( ENUM | UNION ) type_decl_name [ COLON type ] metadata enum_field_open [ enumval_decl ( COMMA enumval_decl )* ] enum_field_close
enum_field_open: OPEN_BLOCK
enum_field_close: CLOSE_BLOCK

root_decl: ROOT_TYPE IDENTIFIER SEMICOLON

type_decl_name: IDENTIFIER

field_decl: field_name COLON type [ EQUAL scalar ] [ optional ] SEMICOLON

field_name: IDENTIFIER

metadata: [ OPEN_PARENTHESIS [ metadata_part ( COMMA metadata_part ) * ] CLOSE_PARENTHESIS ]

metadata_part: IDENTIFIER [ COLON single_value ]

builtin_type: BOOL | BYTE | UBYTE | SHORT | USHORT | INT | UINT | FLOAT | LONG | ULONG | DOUBLE | STRING

type: builtin_type | vector_type | custom_type 

vector_type: OPEN_BRACE type CLOSE_BRACE

custom_type: IDENTIFIER

enumval_decl: IDENTIFIER [ EQUAL INTEGER_LITERAL ]

object:  [ object_part ( COMMA object_part ) * ]

object_part: IDENTIFIER COLON value

scalar: INTEGER_LITERAL | FLOAT_LITERAL | IDENTIFIER | TRUE | FALSE

single_value: scalar | STRING_LITERAL

value: single_value | object | OPEN_BRACE [ value ( COMMA value ) * ] CLOSE_BRACE

optional: OPEN_PARENTHESIS ATTRIBUTE CLOSE_PARENTHESIS 

INCLUDE:            "include"
SEMICOLON:          ";"
NAMESPACE:          "namespace"
DOT:                "."
ATTRIBUTE:          "required" | "deprecated"
ROOT_TYPE:          "root_type"
TABLE:              "table"
STRUCT:             "struct"
OPEN_BLOCK:         "{"         
CLOSE_BLOCK:        "}"         
ENUM:               "enum"
UNION:              "union"
COLON:              ":"
COMMA:              ","
EQUAL:              "="
BOOL:               "bool"
BYTE:               "byte"
UBYTE:              "ubyte"
SHORT:              "short"
USHORT:             "ushort"
INT:                "int"
UINT:               "uint"
FLOAT:              "float"
LONG:               "long"
ULONG:              "ulong"
DOUBLE:             "double"
STRING:             "string"
OPEN_BRACE:         "["
CLOSE_BRACE:         "]"
OPEN_PARENTHESIS:   "("
CLOSE_PARENTHESIS:  ")"
TRUE:               "true"
FALSE:              "false"
COMMENT:            /\/\/(.*\n)/

%import common.INT      -> INTEGER_LITERAL 
%import common.WS       -> WHITESPACE
%import common.FLOAT    -> FLOAT_LITERAL
%import common.CNAME    -> IDENTIFIER
%import common.ESCAPED_STRING -> STRING_LITERAL

%ignore WHITESPACE
%ignore COMMENT

